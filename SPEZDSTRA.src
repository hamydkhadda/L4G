#<AdxTL>@(#)0.0.0.0 $Revision$
##################################################### SPEZDSTRA
##################################################### créé le 26/08/2019
##################################################### modifié le 26/08/2019

$ACTION
#infbox ACTION
Case ACTION
  When "OUVRE" : Gosub OUVRE
  When "DEBUT" : Gosub DEBUT
  When "OK"    : Gosub OK
Endcase
Return

#---------------------------------------------------------------------------------------------------#
#                                           LABELS                                                  #
#---------------------------------------------------------------------------------------------------#

######################################################################### OUVRE
$OUVRE
If clalev([F:ZSTRA])=0 : Local File ZSPETRA [ZSTRA] : Endif
Return

######################################################################### DEBUT
$DEBUT
[M:ZDSTRA]ZARNUM       = PARAM(1)
[M:ZDSTRA]ZCODCHARG    = PARAM(2)
[M:ZDSTRA]ZCODCLTCHARG = PARAM(3)
Affzo [M:ZDSTRA]5
#On va maintenant récupérer les enregistrements dans la table concernant l'article de prestation
Effzo [M:ZDSTRA]10
Raz [M:ZDSTRA]NBLIG
For[F:ZSTRA]ZSTRA0 Where [F:ZSTRA]ZARNUM=[M:ZDSTRA]ZARNUM and [F:ZSTRA]ZCODCHARG=[M:ZDSTRA]ZCODCHARG and [F:ZSTRA]ZCODCLTCHARG=[M:ZDSTRA]ZCODCLTCHARG
  [M:ZDSTRA]ZITMREFORI([M:ZDSTRA]NBLIG)  = [F:ZSTRA]ZITMREFORI
  [M:ZDSTRA]ZTARIF([M:ZDSTRA]NBLIG)      = [F:ZSTRA]ZTARIF
  [M:ZDSTRA]ZCLTFOU([M:ZDSTRA]NBLIG)     = [F:ZSTRA]ZCLTFOU
  [M:ZDSTRA]ZQTYMIN([M:ZDSTRA]NBLIG)     = [F:ZSTRA]ZQTYMIN
  [M:ZDSTRA]ZQTYMAX([M:ZDSTRA]NBLIG)     = [F:ZSTRA]ZQTYMAX
  [M:ZDSTRA]ZCOEFFFAC([M:ZDSTRA]NBLIG)   = [F:ZSTRA]ZCOEFFFAC
  [M:ZDSTRA]NBLIG += 1
Next
Affzo [M:ZDSTRA]10
Return

######################################################################### OK
$OK
#On va sauvegarder les lignes du tableau
#L'opération va se faire à l'intérieure d'une transaction
Local Integer WERR : WERR = 0
Local Integer WNUMLIG : WNUMLIG = 0
Trbegin [F:ZSTRA]
#On commence par supprimer toute les lignes de la table concernant l'article de prestation
Delete [F:ZSTRA] Where [F:ZSTRA]ZARNUM=[M:ZDSTRA]ZARNUM and [F:ZSTRA]ZCODCHARG=[M:ZDSTRA]ZCODCHARG and [F:ZSTRA]ZCODCLTCHARG=[M:ZDSTRA]ZCODCLTCHARG
If(fstat=0)Then
  #On va maintenant parcourir le tableau pour sauvegarder les lignes
  For WNUMLIG=0 To [M:ZDSTRA]NBLIG-1
    [F:ZSTRA]ZARNUM       = [M:ZDSTRA]ZARNUM
    [F:ZSTRA]ZCODCHARG    = [M:ZDSTRA]ZCODCHARG
    [F:ZSTRA]ZCODCLTCHARG = [M:ZDSTRA]ZCODCLTCHARG
    [F:ZSTRA]ZITMREFORI   = [M:ZDSTRA]ZITMREFORI(WNUMLIG)
    [F:ZSTRA]ZCOEFFFAC    = [M:ZDSTRA]ZCOEFFFAC(WNUMLIG)
    [F:ZSTRA]ZTARIF       = [M:ZDSTRA]ZTARIF(WNUMLIG)
    [F:ZSTRA]ZCLTFOU      = [M:ZDSTRA]ZCLTFOU(WNUMLIG)
    [F:ZSTRA]ZQTYMIN      = [M:ZDSTRA]ZQTYMIN(WNUMLIG)
    [F:ZSTRA]ZQTYMAX      = [M:ZDSTRA]ZQTYMAX(WNUMLIG)
    Write[F:ZSTRA]
    If(fstat<>0)Then
      WERR = 1
      Break
    Endif
  Next
Else
  WERR = 1
Endif
If(WERR=0)Then
  Commit
Else
  Rollback
  Call ERREUR("Impossible de sauvegarder les lignes !!") From GESECRAN
Endif
Return

#---------------------------------------------------------------------------------------------------#
#                                           SUBPROGS                                                #
#---------------------------------------------------------------------------------------------------#

Subprog C_ZPAYS(VALEUR)
Variable Char    VALEUR()
#On va controler que la valeur du code pays est bien dans le vue de sélection
If(VALEUR<>"")Then
  If clalev([F:ZSCRY])=0 : Local File ZSELCRY [ZSCRY] : Endif
  Read[F:ZSCRY]ZSCRY0=VALEUR
  If(fstat<>0)Then
    mkstat = 2
    GMESSAGE = "Impossible, ce code pays n'existe pas."
  Endif
Endif
End

Subprog C_ZSITE(VALEUR)
Variable Char    VALEUR()
#On va controler que la valeur du site est bien dans le vue de sélection
If(VALEUR<>"")Then
  If clalev([F:ZSFCY])=0 : Local File ZSELFCY [ZSFCY] : Endif
  Read[F:ZSFCY]ZSFCY0=VALEUR
  If(fstat<>0)Then
    mkstat = 2
    GMESSAGE = "Impossible, ce code site n'existe pas."
  Endif
Endif
End

Subprog C_ZITMREFORI(VALEUR)
Variable Char    VALEUR()
#On va controler que la valeur du code article de tri est bien dans le vue de sélection
If(VALEUR<>"")Then
  If clalev([F:ZSART])=0 : Local File ZSELARTTRI [ZSART] : Endif
  Read[F:ZSART]ZSART=[M:ZDSTRA]ZCODCHARG;VALEUR
  If(fstat<>0)Then
    mkstat = 2
    GMESSAGE = "Impossible, ce code article n'existe pas pour ce chargeur."
  Endif
Endif
End
