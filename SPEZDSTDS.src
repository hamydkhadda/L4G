#<AdxTL>@(#)0.0.0.0 $Revision$
##################################################### SPEZDSTDS
##################################################### créé le 26/08/2019
##################################################### modifié le 26/08/2019

$ACTION
#infbox ACTION
Case ACTION
  When "OUVRE" : Gosub OUVRE
  When "DEBUT" : Gosub DEBUT
  When "OK"    : Gosub OK
Endcase
Return

#---------------------------------------------------------------------------------------------------#
#                                           LABELS                                                  #
#---------------------------------------------------------------------------------------------------#

######################################################################### OUVRE
$OUVRE
If clalev([F:ZSTDS])=0 : Local File ZSPETDS [ZSTDS] : Endif
Return

######################################################################### DEBUT
$DEBUT
[M:ZDSTDS]ZARNUM       = PARAM(1)
[M:ZDSTDS]ZCODCHARG    = PARAM(2)
[M:ZDSTDS]ZCODCLTCHARG = PARAM(3)
Affzo [M:ZDSTDS]5
#On va maintenant récupérer les enregistrements dans la table concernant l'article de prestation
Effzo [M:ZDSTDS]10
Raz [M:ZDSTDS]NBLIG
For[F:ZSTDS]ZSTDS0 Where [F:ZSTDS]ZARNUM=[M:ZDSTDS]ZARNUM and [F:ZSTDS]ZCODCHARG=[M:ZDSTDS]ZCODCHARG and [F:ZSTDS]ZCODCLTCHARG=[M:ZDSTDS]ZCODCLTCHARG
  [M:ZDSTDS]ZSITEORI([M:ZDSTDS]NBLIG)  = [F:ZSTDS]ZSITEORI
  [M:ZDSTDS]ZSITEDEST([M:ZDSTDS]NBLIG) = [F:ZSTDS]ZSITEDEST
  [M:ZDSTDS]ZTARIF([M:ZDSTDS]NBLIG)    = [F:ZSTDS]ZTARIF
  [M:ZDSTDS]ZCLTFOU([M:ZDSTDS]NBLIG)   = [F:ZSTDS]ZCLTFOU
  [M:ZDSTDS]ZQTYMIN([M:ZDSTDS]NBLIG)   = [F:ZSTDS]ZQTYMIN
  [M:ZDSTDS]ZQTYMAX([M:ZDSTDS]NBLIG)   = [F:ZSTDS]ZQTYMAX
  [M:ZDSTDS]ZTYPPAL([M:ZDSTDS]NBLIG)   = [F:ZSTDS]ZTYPPAL
  [M:ZDSTDS]ZARTVEN([M:ZDSTDS]NBLIG)   = [F:ZSTDS]ZARTVEN
  [M:ZDSTDS]ZARTACH([M:ZDSTDS]NBLIG)   = [F:ZSTDS]ZARTACH
  [M:ZDSTDS]NBLIG += 1
Next
Affzo [M:ZDSTDS]10
Return

######################################################################### OK
$OK
#On va sauvegarder les lignes du tableau
#L'opération va se faire à l'intérieure d'une transaction
Local Integer WERR : WERR = 0
Local Integer WNUMLIG : WNUMLIG = 0
Trbegin [F:ZSTDS]
#On commence par supprimer toute les lignes de la table concernant l'article de prestation
Delete [F:ZSTDS] Where [F:ZSTDS]ZARNUM=[M:ZDSTDS]ZARNUM and [F:ZSTDS]ZCODCHARG=[M:ZDSTDS]ZCODCHARG and [F:ZSTDS]ZCODCLTCHARG=[M:ZDSTDS]ZCODCLTCHARG
If(fstat=0)Then
  #On va maintenant parcourir le tableau pour sauvegarder les lignes
  For WNUMLIG=0 To [M:ZDSTDS]NBLIG-1
    [F:ZSTDS]ZARNUM       = [M:ZDSTDS]ZARNUM
    [F:ZSTDS]ZCODCHARG    = [M:ZDSTDS]ZCODCHARG
    [F:ZSTDS]ZCODCLTCHARG = [M:ZDSTDS]ZCODCLTCHARG
    [F:ZSTDS]ZSITEORI     = [M:ZDSTDS]ZSITEORI(WNUMLIG)
    [F:ZSTDS]ZSITEDEST    = [M:ZDSTDS]ZSITEDEST(WNUMLIG)
    [F:ZSTDS]ZTARIF       = [M:ZDSTDS]ZTARIF(WNUMLIG)
    [F:ZSTDS]ZCLTFOU      = [M:ZDSTDS]ZCLTFOU(WNUMLIG)
    [F:ZSTDS]ZQTYMIN      = [M:ZDSTDS]ZQTYMIN(WNUMLIG)
    [F:ZSTDS]ZQTYMAX      = [M:ZDSTDS]ZQTYMAX(WNUMLIG)
    [F:ZSTDS]ZTYPPAL      = [M:ZDSTDS]ZTYPPAL(WNUMLIG)
    [F:ZSTDS]ZARTVEN      = [M:ZDSTDS]ZARTVEN(WNUMLIG)
    [F:ZSTDS]ZARTACH      = [M:ZDSTDS]ZARTACH(WNUMLIG)
    Write[F:ZSTDS]
    If(fstat<>0)Then
      WERR = 1
      Break
    Endif
  Next
Else
  WERR = 1
Endif
If(WERR=0)Then
  Commit
Else
  Rollback
  Call ERREUR("Impossible de sauvegarder les lignes !!") From GESECRAN
Endif
Return

#---------------------------------------------------------------------------------------------------#
#                                           SUBPROGS                                                #
#---------------------------------------------------------------------------------------------------#

Subprog C_ZPAYS(VALEUR)
Variable Char    VALEUR()
#On va controler que la valeur du code pays est bien dans le vue de sélection
If(VALEUR<>"")Then
  If clalev([F:ZSCRY])=0 : Local File ZSELCRY [ZSCRY] : Endif
  Read[F:ZSCRY]ZSCRY0=VALEUR
  If(fstat<>0)Then
    mkstat = 2
    GMESSAGE = "Impossible, ce code pays n'existe pas."
  Endif
Endif
End

Subprog C_ZSITEORI(VALEUR)
Variable Char    VALEUR()
#On va controler que la valeur du site est bien dans le vue de sélection
If(VALEUR<>"")Then
  If clalev([F:ZSFCY])=0 : Local File ZSELFCY [ZSFCY] : Endif
  Read[F:ZSFCY]ZSFCY0=VALEUR
  If(fstat<>0)Then
    mkstat = 2
    GMESSAGE = "Impossible, ce code site n'existe pas."
  Endif
Endif
End

Subprog C_ZSITEDEST(VALEUR)
Variable Char    VALEUR()
#On va controler que la valeur du site est bien dans le vue de sélection
If(VALEUR<>"")Then
  If clalev([F:ZSFCY])=0 : Local File ZSELFCY [ZSFCY] : Endif
  Read[F:ZSFCY]ZSFCY0=VALEUR
  If(fstat<>0)Then
    mkstat = 2
    GMESSAGE = "Impossible, ce code site n'existe pas."
  Endif
Endif
End
